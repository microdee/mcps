#include "mcps.fxh"
#define PI 3.1415926535897932
#define GEOMVEL "SV_Position.xyz;NORMAL.xyz;TEXCOORD0.xy;COLOR0"
// mcps

StructuredBuffer<float> McpsData;
StructuredBuffer<float> PrevMcpsData;
/*
size	offset	name		note
3		0		position	
3		3		velocity
3		7		force
4		10		color
1		14		size		Radius1
2		15		age
ext:
4		17		Rotation	Quaternion
1		21		Radius2
1		22		Thickness	
1		23		Depth
1		24		Cycles
1		25		Phase
1		26		Power
1		27		Curvature
*/
//ext:
float4 mcps_Rotation(StructuredBuffer<float> mcps, uint i) {return normalize(float4(mcps[i*pelsize+17],mcps[i*pelsize+18],mcps[i*pelsize+19],mcps[i*pelsize+20]));}
float mcps_Radius2(StructuredBuffer<float> mcps, uint i) {return mcps[i*pelsize+21];}
float mcps_Thickness(StructuredBuffer<float> mcps, uint i) {return mcps[i*pelsize+22];}
float mcps_Depth(StructuredBuffer<float> mcps, uint i) {return mcps[i*pelsize+23];}
float mcps_Cycles(StructuredBuffer<float> mcps, uint i) {return mcps[i*pelsize+24];}
float mcps_Phase(StructuredBuffer<float> mcps, uint i) {return mcps[i*pelsize+25];}
float mcps_Power(StructuredBuffer<float> mcps, uint i) {return mcps[i*pelsize+26];}
float mcps_Curvature(StructuredBuffer<float> mcps, uint i) {return mcps[i*pelsize+27];}

StructuredBuffer<float> Linear;
float GrowTime = 1;

struct VsOutGeomvel
{
	float4 cpoint : SV_Position;
	float3 norm : NORMAL;
	float TexCd : TEXCOORD0;
	float4 ppos : COLOR0;
	float3 bufferdata : TEXCOORD1;
};
float PowS(float a, float b)
{
	return pow(abs(a),b)*sign(a);
}
VsOutGeomvel VsGeomvel(uint vi: SV_VertexID, uint ii: SV_InstanceID)
{
    VsOutGeomvel output;
	uint LinearCount, LinearStr;
	Linear.GetDimensions(LinearCount, LinearStr);
	
	//// current ////
	
	float3 circpos = 0;
	float d = gmcps_size(McpsData, ii);
	float phase = mcps_Phase(McpsData, ii);
	float cycles = mcps_Cycles(McpsData, ii);
	float r = Linear[vi]*PI*cycles*2+phase*2*PI;
	circpos.x = lerp(PowS(cos(r),mcps_Power(McpsData, ii))*d, Linear[vi]-.5, mcps_Curvature(McpsData, ii));
	circpos.y = lerp(PowS(sin(r),mcps_Power(McpsData, ii))*d, 0, mcps_Curvature(McpsData, ii));
	circpos.z = 0;
	float3 norm = 0;
	norm.x = lerp(cos(r), 0, mcps_Curvature(McpsData, ii));
	norm.y = lerp(sin(r), -1, mcps_Curvature(McpsData, ii));
	
	//// previous ////
	float3 pcircpos = 0;
	float pd = gmcps_size(PrevMcpsData, ii);
	float pphase = mcps_Phase(PrevMcpsData, ii);
	float pcycles = mcps_Cycles(PrevMcpsData, ii);
	float pr = Linear[vi]*PI*pcycles*2+pphase*2*PI;
	pcircpos.x = lerp(PowS(cos(pr),mcps_Power(PrevMcpsData, ii))*pd, Linear[vi]-.5, mcps_Curvature(PrevMcpsData, ii));
	pcircpos.y = lerp(PowS(sin(pr),mcps_Power(PrevMcpsData, ii))*pd, 0, mcps_Curvature(PrevMcpsData, ii));
	pcircpos.z = 0;
	
	//// out ////
	
    output.cpoint = float4(circpos,1);
	output.norm = norm;
	output.TexCd = Linear[vi];
	output.ppos = float4(pcircpos,ii);
	output.bufferdata = float3(pelsize, LinearCount, vi);
	
    return output;
}

struct GsOutGeomvel
{
	float4 cpoint : SV_Position;
	float3 norm : NORMAL;
	float4 TexCd : TEXCOORD0;
	float4 ppos : COLOR0;
};

struct GStmp
{
	float3 pos;
	float2 txcd;
};
float4x4 QRotate(float4 q)
{
	float4x4 mat = {
		1,0,0,0,
		0,1,0,0,
		0,0,1,0,
		0,0,0,1};
	mat[0][0] = 1 - 2*q.y*q.y - 2*q.z*q.z;
	mat[0][1] = 2*q.x*q.y + 2*q.w*q.z;
	mat[0][2] = 2*q.x*q.z - 2*q.w*q.y;
	mat[1][0] = 2*q.x*q.y - 2*q.w*q.z;
	mat[1][1] = 1 - 2*q.x*q.x - 2*q.z*q.z;
	mat[1][2] = 2*q.y*q.z + 2*q.w*q.x;
	mat[2][0] = 2*q.x*q.z + 2*q.w*q.y;
	mat[2][1] = 2*q.y*q.z - 2*q.w*q.x;
	mat[2][2] = 1 - 2*q.x*q.x - 2*q.y*q.y;
	return mat;
}
[maxvertexcount(14)]
void GsGeomvel(line VsOutGeomvel input[2], inout TriangleStream<GsOutGeomvel>GSOut)
{
	uint ii = input[0].ppos.w;
	uint s = input[0].bufferdata.x;

	//// current ////

	GStmp evenring[4];
	// pos
	evenring[0].pos = input[0].cpoint.xyz;
	evenring[1].pos = input[0].cpoint.xyz + input[0].norm * mcps_Thickness(McpsData, ii);
	evenring[2].pos = input[0].cpoint.xyz * mcps_Radius2(McpsData, ii) + input[0].norm * mcps_Thickness(McpsData, ii);
	evenring[2].pos.z += mcps_Depth(McpsData, ii);
	evenring[3].pos = input[0].cpoint.xyz * mcps_Radius2(McpsData, ii);
	evenring[3].pos.z += mcps_Depth(McpsData, ii);
	// txcd
	evenring[0].txcd = float2(input[0].TexCd, 0);
	evenring[1].txcd = float2(input[0].TexCd, 1);
	evenring[2].txcd = float2(input[0].TexCd, 0);
	evenring[3].txcd = float2(input[0].TexCd, 1);
	
	GStmp oddring[4];
	// pos
	oddring[0].pos = input[1].cpoint.xyz;
	oddring[1].pos = input[1].cpoint.xyz + input[1].norm * mcps_Thickness(McpsData, ii);
	oddring[2].pos = input[1].cpoint.xyz * mcps_Radius2(McpsData, ii) + input[1].norm * mcps_Thickness(McpsData, ii);
	oddring[2].pos.z += mcps_Depth(McpsData, ii);
	oddring[3].pos = input[1].cpoint.xyz * mcps_Radius2(McpsData, ii);
	oddring[3].pos.z += mcps_Depth(McpsData, ii);
	// txcd
	oddring[0].txcd = float2(input[1].TexCd, 0);
	oddring[1].txcd = float2(input[1].TexCd, 1);
	oddring[2].txcd = float2(input[1].TexCd, 0);
	oddring[3].txcd = float2(input[1].TexCd, 1);

	// norm
	float3 eosnorm = 0;
	float3 eosnorm0 = evenring[0].pos-evenring[3].pos;
	float3 eosnorm1 = evenring[0].pos-oddring[3].pos;
	eosnorm = normalize(cross(eosnorm0,eosnorm1));
	float3 eisnorm = 0;
	float3 eisnorm0 = evenring[1].pos-evenring[2].pos;
	float3 eisnorm1 = evenring[1].pos-oddring[2].pos;
	eisnorm = normalize(cross(eisnorm0,eisnorm1));
	float3 efwnorm = 0;
	float3 efwnorm0 = evenring[0].pos-evenring[1].pos;
	float3 efwnorm1 = evenring[0].pos-oddring[1].pos;
	efwnorm = normalize(cross(efwnorm0,efwnorm1));
	float3 ebwnorm = 0;
	float3 ebwnorm0 = evenring[2].pos-evenring[3].pos;
	float3 ebwnorm1 = evenring[2].pos-oddring[3].pos;
	ebwnorm = normalize(cross(ebwnorm0,ebwnorm1));

	float3 oosnorm = 0;
	float3 oosnorm0 = oddring[0].pos-oddring[3].pos;
	float3 oosnorm1 = oddring[0].pos-evenring[3].pos;
	oosnorm = normalize(cross(oosnorm0,oosnorm1));
	float3 oisnorm = 0;
	float3 oisnorm0 = oddring[1].pos-oddring[2].pos;
	float3 oisnorm1 = oddring[1].pos-evenring[2].pos;
	oisnorm = normalize(cross(oisnorm0,oisnorm1));
	float3 ofwnorm = 0;
	float3 ofwnorm0 = oddring[0].pos-oddring[1].pos;
	float3 ofwnorm1 = oddring[0].pos-evenring[1].pos;
	ofwnorm = normalize(cross(ofwnorm0,ofwnorm1));
	float3 obwnorm = 0;
	float3 obwnorm0 = oddring[2].pos-oddring[3].pos;
	float3 obwnorm1 = oddring[2].pos-evenring[3].pos;
	obwnorm = normalize(cross(obwnorm0,obwnorm1));

	//// previous ////

	float3 pevenring[4];
	pevenring[0] = input[0].ppos.xyz;
	pevenring[1] = input[0].ppos.xyz + input[0].norm * mcps_Thickness(PrevMcpsData, ii);
	pevenring[2] = input[0].ppos.xyz * mcps_Radius2(PrevMcpsData, ii) + input[0].norm * mcps_Thickness(PrevMcpsData, ii);
	pevenring[2].z += mcps_Depth(PrevMcpsData, ii);
	pevenring[3] = input[0].ppos.xyz * mcps_Radius2(PrevMcpsData, ii);
	pevenring[3].z += mcps_Depth(PrevMcpsData, ii);
	
	float3 poddring[4];
	poddring[0] = input[1].ppos.xyz;
	poddring[1] = input[1].ppos.xyz + input[1].norm * mcps_Thickness(PrevMcpsData, ii);
	poddring[2] = input[1].ppos.xyz * mcps_Radius2(PrevMcpsData, ii) + input[1].norm * mcps_Thickness(PrevMcpsData, ii);
	poddring[2].z += mcps_Depth(PrevMcpsData, ii);
	poddring[3] = input[1].ppos.xyz * mcps_Radius2(PrevMcpsData, ii);
	poddring[3].z += mcps_Depth(PrevMcpsData, ii);
	
	float grow = smoothstep(0,1,saturate(gmcps_age(McpsData, ii).y/GrowTime));
	float pgrow = smoothstep(0,1,saturate(gmcps_age(PrevMcpsData, ii).y/GrowTime));
	//// transform ////
	for(uint i=0; i<4; i++)
	{
		evenring[i].pos = mul(float4(evenring[i].pos,1),QRotate(mcps_Rotation(McpsData, ii))).xyz;
		evenring[i].pos *= grow;
		evenring[i].pos += gmcps_position(McpsData, ii);
		oddring[i].pos = mul(float4(oddring[i].pos,1),QRotate(mcps_Rotation(McpsData, ii))).xyz;
		oddring[i].pos *= grow;
		oddring[i].pos += gmcps_position(McpsData, ii);
		
		pevenring[i] = mul(float4(pevenring[i],1),QRotate(mcps_Rotation(PrevMcpsData, ii))).xyz;
		pevenring[i] *= pgrow;
		pevenring[i] += gmcps_position(PrevMcpsData, ii);
		poddring[i] = mul(float4(poddring[i],1),QRotate(mcps_Rotation(PrevMcpsData, ii))).xyz;
		poddring[i] *= pgrow;
		poddring[i] += gmcps_position(PrevMcpsData, ii);
	}
	if(gmcps_age(McpsData, ii).x == 1)
	{
		for(uint i=0; i<4; i++)
		{
			pevenring[i] = evenring[i].pos;
			poddring[i] = oddring[i].pos;
		}
	}
	//// join ////

	GsOutGeomvel v = (GsOutGeomvel)0;
	GsOutGeomvel vle = (GsOutGeomvel)0;
	GsOutGeomvel vlo = (GsOutGeomvel)0;
	// ....
	// o...
	v.cpoint = float4(evenring[0].pos,1);
	v.norm =   (eosnorm+efwnorm)/2;
	v.TexCd =  float4(evenring[0].txcd,0,1);
	v.ppos =   float4(pevenring[0],ii);
	vle = v;
	GSOut.Append(v);
	// o...
	// ....
	v.cpoint = float4(oddring[0].pos,1);
	v.norm =   (-oosnorm-ofwnorm)/2;
	v.TexCd =  float4(oddring[0].txcd,0,1);
	v.ppos =   float4(poddring[0],ii);
	vlo = v;
	GSOut.Append(v);
	// ....
	// .o..
	v.cpoint = float4(evenring[1].pos,1);
	v.norm =   (eisnorm+efwnorm)/2;
	v.TexCd =  float4(evenring[1].txcd,0,1);
	v.ppos =   float4(pevenring[1],ii);
	GSOut.Append(v);
	// .o..
	// ....
	v.cpoint = float4(oddring[1].pos,1);
	v.norm =   (-oisnorm-ofwnorm)/2;
	v.TexCd =  float4(oddring[1].txcd,0,1);
	v.ppos =   float4(poddring[1],ii);
	GSOut.Append(v);
	// ....
	// ..o.
	v.cpoint = float4(evenring[2].pos,1);
	v.norm =   (eisnorm+ebwnorm)/2;
	v.TexCd =  float4(evenring[2].txcd,0,1);
	v.ppos =   float4(pevenring[2],ii);
	GSOut.Append(v);
	// ..o.
	// ....
	v.cpoint = float4(oddring[2].pos,1);
	v.norm =   (-oisnorm-obwnorm)/2;
	v.TexCd =  float4(oddring[2].txcd,0,1);
	v.ppos =   float4(poddring[2],ii);
	GSOut.Append(v);
	// ....
	// ...o
	v.cpoint = float4(evenring[3].pos,1);
	v.norm =   (eosnorm+ebwnorm)/2;
	v.TexCd =  float4(evenring[3].txcd,0,1);
	v.ppos =   float4(pevenring[3],ii);
	GSOut.Append(v);
	// ...o
	// ....
	v.cpoint = float4(oddring[3].pos,1);
	v.norm =   (-oosnorm-obwnorm)/2;
	v.TexCd =  float4(oddring[3].txcd,0,1);
	v.ppos =   float4(poddring[3],ii);
	GSOut.Append(v);
	GSOut.Append(vle);
	GSOut.Append(vlo);
	GSOut.RestartStrip();
	if(input[0].bufferdata.z == 0)
	{
		float4 jump = float4(1,2,-1,-1);
		uint wi = 0;
		for(uint i=0; i<4; i++)
		{
			v.cpoint = float4(evenring[wi].pos,1);
			v.norm =   float3(0,0,1);
			v.TexCd =  float4(evenring[wi].txcd,0,1);
			v.ppos =   float4(pevenring[wi],ii);
			GSOut.Append(v);
			wi += jump[i];
		}
	}
	if(input[1].bufferdata.z == (input[1].bufferdata.y-1))
	{
		float4 jump = float4(1,2,-1,-1);
		uint wi = 0;
		for(uint i=0; i<4; i++)
		{
			v.cpoint = float4(oddring[wi].pos,1);
			v.norm =   float3(0,0,1);
			v.TexCd =  float4(oddring[wi].txcd,0,1);
			v.ppos =   float4(poddring[wi],ii);
			GSOut.Append(v);
			wi += jump[i];
		}
	}
}
GeometryShader StreamGeomvel = ConstructGSWithSO( CompileShader( gs_5_0, GsGeomvel() ), GEOMVEL );
technique11 GeomvelLayout
{
	pass P0
	{
		
		SetVertexShader( CompileShader( vs_5_0, VsGeomvel() ) );
		SetGeometryShader( CompileShader( gs_5_0, GsGeomvel() ) );
	    SetGeometryShader( StreamGeomvel );

	}
}