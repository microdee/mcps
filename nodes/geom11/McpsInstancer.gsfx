#include "../../../mp.fxh/mcpsRead.fxh"
#include "../../../mp.fxh/pows.fxh"
#include "../../../mp.fxh/quaternion.fxh"

//define STACK
//define STACKTRI

#if !defined(STACK)
#define STACK 1
#define STACKTRI 3
#endif

#if !defined(SUBSETID_OUT)
#define SUBSETID_OUT 1
#endif

#include "../../../mp.fxh/MREInputElements.fxh"

ByteAddressBuffer McpsData;
ByteAddressBuffer PrevMcpsData;

float4x4 PreTransform;

GSin VS(VSin input)
{
    GSin output;

	uint ci = input.iid * STACK;

    output.cpoint = mul(float4(input.cpoint, 1), PreTransform).xyz;
	output.norm = mul(float4(input.norm, 0), PreTransform).xyz;
    #if defined(PREVPOS_OUT)
	    #if defined(PREVPOS_IN)
	        output.PrevPos = mul(float4(input.PrevPos, 1), PreTransform).xyz;
	    #else
	        output.PrevPos = output.cpoint;
	    #endif
	#endif
    output.SubsetID = ci;

    #if defined(TEXCOORD_IN) && defined(TEXCOORD_OUT)
        output.TexCd = input.TexCd;
    #endif
    #if defined(TANGENTS_IN) && defined(TANGENTS_OUT)
        output.Tangent = mul(float4(input.Tangent, 0), PreTransform).xyz;
        output.Binormal = mul(float4(input.Binormal, 0), PreTransform).xyz;
    #endif
    #if defined(BLENDWEIGHTS_IN) && defined(BLENDWEIGHTS_OUT)
        output.BlendId = input.BlendId;
        output.BlendWeight = input.BlendWeight;
    #endif

    return output;
}

[maxvertexcount(STACKTRI)]
void GS(triangle GSin input[3], inout TriangleStream<GSin>GSOut)
{
	GSin o = (GSin)0;
	[unroll]
	for(uint i=0; i<STACK; i++)
	{
		uint pi = input[0].SubsetID + i;

		//// current ////
		float3 pos = mcpsPositionLoad(McpsData, pi);
		float2 age = mcpsAgeLoad(McpsData, pi);
		#if defined(PREVPOS_OUT)
			float3 ppos = mcpsPositionLoad(PrevMcpsData, pi);
			float2 page = mcpsAgeLoad(PrevMcpsData, pi);
		#endif

        #if defined(KNOW_SIZE)
    		float size = mcpsSizeLoad(McpsData, pi);
			#if defined(PREVPOS_OUT)
            	float psize = mcpsSizeLoad(PrevMcpsData, pi);
			#endif
        #endif
        #if defined(KNOW_SCALE)
    		float3 scale = mcpsScaleLoad(McpsData, pi);
			#if defined(PREVPOS_OUT)
            	float3 pscale = mcpsScaleLoad(PrevMcpsData, pi);
			#endif
        #endif
        #if defined(KNOW_ROTATION)
    		float4 rot = mcpsRotationLoad(McpsData, pi);
    		float4x4 rotm = qrot(rot);
			#if defined(PREVPOS_OUT)
	    		float4 prot = mcpsRotationLoad(PrevMcpsData, pi);
	    		float4x4 protm = qrot(prot);
			#endif
        #endif

		[unroll]
		for(uint j=0; j<3; j++)
		{
			//// current ////
			float3 cpos = input[j].cpoint;
			#if defined(PREVPOS_OUT)
				float3 pcpos = input[j].PrevPos;
			#endif
			float3 cnorm = input[j].norm;
            #if defined(TANGENTS_IN) && defined(TANGENTS_OUT)
                float3 tangent = input[j].Tangent;
                float3 binormal = input[j].Binormal;
            #endif
            #if defined(KNOW_SIZE)
                cpos.xyz *= size;
				#if defined(PREVPOS_OUT)
    				pcpos.xyz *= psize;
				#endif
            #endif
            #if defined(KNOW_SCALE)
                cpos.xyz *= scale;
				#if defined(PREVPOS_OUT)
    				pcpos.xyz *= pscale;
				#endif
            #endif
            #if defined(KNOW_ROTATION)
                cpos = mul(float4(cpos,1), rotm).xyz;
				#if defined(PREVPOS_OUT)
    				pcpos = mul(pcpos, protm);
				#endif
                cnorm = mul(float4(cnorm, 0), rotm).xyz;
                #if defined(TANGENTS_IN) && defined(TANGENTS_OUT)
                    tangent = mul(float4(tangent, 0), rotm).xyz;
                    binormal = mul(float4(binormal, 0), rotm).xyz;
                #endif
            #endif

			cpos += pos;
			#if defined(PREVPOS_OUT)
				pcpos += ppos;
            	o.PrevPos = pcpos;
				if(age.x < 2) o.PrevPos = o.cpoint;
			#endif
            o.cpoint = cpos;
            o.norm = cnorm;

            #if defined(TEXCOORD_IN) && defined(TEXCOORD_OUT)
                o.TexCd = input[j].TexCd;
            #endif
            #if defined(TANGENTS_IN) && defined(TANGENTS_OUT)
                o.Tangent = tangent;
                o.Binormal = binormal;
            #endif
            #if defined(BLENDWEIGHTS_IN) && defined(BLENDWEIGHTS_OUT)
                o.BlendId = input[j].BlendId;
                o.BlendWeight = input[j].BlendWeight;
            #endif

			o.SubsetID = pi;
			GSOut.Append(o);
		}
		GSOut.RestartStrip();
	}
}

GeometryShader StreamOutGS = ConstructGSWithSO( CompileShader( gs_5_0, GS() ),
	"POSITION.xyz;"
	"NORMAL.xyz"
	#if defined(TEXCOORD_OUT)
		";TEXCOORD0.xy"
	#endif
	#if defined(TANGENTS_OUT)
		";TANGENT.xyz"
		";BINORMAL.xyz"
	#endif
	#if defined(BLENDWEIGHTS_OUT)
		";BLENDINDICES.xyzw"
		";BLENDWEIGHT.xyzw"
	#endif
	#if defined(PREVPOS_OUT)
		";PREVPOS.xyz"
	#endif
	#if defined(SUBSETID_OUT)
		";SUBSETID.x"
	#endif
);

technique11 geom
{
	pass P0
	{

		SetVertexShader( CompileShader( vs_5_0, VS() ) );
		SetGeometryShader( CompileShader( gs_5_0, GS() ) );
	    SetGeometryShader( StreamOutGS );
	}
}
